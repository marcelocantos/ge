plugins {
    id 'com.android.application'
}

android {
    namespace "com.squz.player"
    compileSdk 35

    defaultConfig {
        applicationId "com.squz.player"
        minSdk 26
        targetSdk 35
        versionCode 1
        versionName "1.0"

        ndk {
            abiFilters 'arm64-v8a'
        }

        externalNativeBuild {
            cmake {
                arguments "-DANDROID_STL=c++_shared"
            }
        }
    }

    signingConfigs {
        release {
            def props = new Properties()
            def propsFile = rootProject.file("local.properties")
            if (propsFile.exists()) props.load(propsFile.newDataInputStream())
            storeFile file(props.getProperty("RELEASE_STORE_FILE", "/dev/null"))
            storePassword props.getProperty("RELEASE_STORE_PASSWORD", "")
            keyAlias props.getProperty("RELEASE_KEY_ALIAS", "")
            keyPassword props.getProperty("RELEASE_KEY_PASSWORD", "")
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            signingConfig signingConfigs.release
        }
    }

    externalNativeBuild {
        cmake {
            path 'src/main/cpp/CMakeLists.txt'
        }
    }

    // Include SDL3 Java sources from vendor
    sourceSets {
        main {
            java.srcDirs = [
                'src/main/java',
                "${rootDir}/../../vendor/github.com/libsdl-org/SDL/android-project/app/src/main/java"
            ]
        }
    }

    lint {
        abortOnError = false
    }
}

dependencies {
    implementation 'com.google.android.gms:play-services-code-scanner:16.1.0'
}
